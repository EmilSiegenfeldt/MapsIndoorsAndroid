buildscript {
    repositories {
        maven { url 'https://maven.fabric.io/public' }
    }

    dependencies {
        classpath "io.fabric.tools:gradle:1.28.1"
    }
}

apply plugin: 'com.android.application'
apply plugin: 'io.fabric'

// Add the MapsIndoors plugin to embed the "offline" data
apply from: 'https://raw.githubusercontent.com/MapsIndoors/MapsIndoorsAndroid/master/gradle/MapsIndoorsOfflineDataSync_v2.gradle'
preBuild.dependsOn fetchData

repositories {
    flatDir { dirs 'libs' }
    mavenCentral()
    maven { url 'https://maven.fabric.io/public' }
}

android {
    signingConfigs {
        miDemo {
            keyAlias 'put_your_own_key'
            keyPassword 'put_your_own_key_password'
            storeFile file('put_your_own_store_file')
            storePassword 'put_your_own_store_password'
        }
    }

    lintOptions {
        disable "ResourceType"

        checkReleaseBuilds false

        check 'Interoperability' // Ref: https://developer.android.com/studio/preview/features/index.html#java_kotlin_interop

        htmlReport true
        htmlOutput file("${buildDir}/reports/lint/lint.html")
    }

    compileSdkVersion 27
    buildToolsVersion '28.0.3'

    //
    defaultConfig {

        minSdkVersion    21
        targetSdkVersion 27

        vectorDrawables.useSupportLibrary = true
    }

    buildTypes {
        release {
            minifyEnabled true
            zipAlignEnabled true
            shrinkResources true

            proguardFile 'proguard-rules.pro'
            proguardFile getDefaultProguardFile('proguard-android-optimize.txt')
        }

        debug {
            debuggable true

            ext.enableCrashlytics = false
            ext.alwaysUpdateBuildId = false
        }
    }

    // Specifies a flavor dimension.
    flavorDimensions "default"

    productFlavors {

        miDemo {
            dimension "default"
           // signingConfig signingConfigs.miDemo
            applicationId "com.mapsindoors.mapsindoorsdemo"
            versionCode 21
            versionName '2.1.0'

            // check: https://developer.android.com/studio/build/shrink-code#unused-alt-resources
            resConfigs "en", "da"
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    bundle {
        density.enableSplit = true
        abi.enableSplit = true
        language.enableSplit = false
    }
}

dependencies {
    implementation fileTree(include: ['*.jar'], dir: 'libs')

    implementation "com.android.support:appcompat-v7:27.1.1"
    implementation "com.android.support:design:27.1.1"
    implementation "com.android.support:cardview-v7:27.1.1"
    implementation "com.android.support.constraint:constraint-layout:1.1.3"

    implementation "com.mapspeople.mapsindoors:mapsindoorssdk:2.1.4@aar"
    implementation "com.google.code.gson:gson:2.8.5"

    implementation "org.altbeacon:android-beacon-library:2.12.4"

    implementation 'com.google.android.gms:play-services-maps:16.1.0'
    implementation 'com.google.android.gms:play-services-location:16.0.0'
    implementation 'com.google.firebase:firebase-core:16.0.8'

    implementation('com.crashlytics.sdk.android:crashlytics:2.9.9@aar') {transitive = true;}
    implementation "com.squareup.picasso:picasso:2.71828"
    implementation "uk.co.chrisjenx:calligraphy:2.3.0"
    implementation "com.karumi:dexter:4.2.0"

    // DEV ONLY
    debugImplementation 'com.squareup.leakcanary:leakcanary-android:1.6.3'
    releaseImplementation 'com.squareup.leakcanary:leakcanary-android-no-op:1.6.3'
    testImplementation 'com.squareup.leakcanary:leakcanary-android-no-op:1.6.3'
}

gradle.projectsEvaluated {
    tasks.withType(JavaCompile) {
        options.compilerArgs << "-Xlint:deprecation" << "-Xlint:unchecked"
    }
}

// Google Services, placed at the bottom to avoid a "version conflict" error when building, solution is mentioned here: http://stackoverflow.com/questions/34370603/version-conflict-updating-to-8-4-0
apply plugin: 'com.google.gms.google-services'
